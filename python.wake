############################################################################
#
# Python-Wake
#
# Supports multiple Python virtual environments through the "pipenv" mechanism.
#   Each environment corresponds to a directory containing a Pipfile.
#
# To use the python virtual environment, add it to the command's plan.
# For example,
#     makePlan  (pythonCommand  "myscript.py" (arg1, ar2, Nil))  visibles
#     | addPythonEnv  "directory containing Pipfile"
#     | runJob
#
# If you have a specific python module to run, you can create the plan like this:
#     makePlan (pythonModule my.module (arglist))  sources
#
# If you wish to download python modules ahead of time,
#      publish preinstall = (pythonInstaller "directory-containing-Pipfile"), Nil
# and he "wake preinstall Unit" call will then download the python modules.
#
#############################################################################



######################################################
# Add a Python virtual environment to a plan.
#   Currently only supports pipenv, but this is where would examine the directory
#   to see if other env managers are being used.
##########################################################
global def addPythonEnv pipDir plan =

  # Install the virtual environment if not already done
  def installed = installPipenvEnv pipDir
  def venv = "build/{pipDir}/.venv"

  # Add the binary directory to PATH.
  plan
  | editPlanVisible (installed ++ _)
  | addPlanEnvironmentPath  "PYTHONPATH"  venv
  | addPlanEnvironmentPath  "PATH" "{venv}/bin"


###########################################################################
# Create an installer function which, when invoked, installs python virtual env.
#    Note the extra "Unit" in the argument list - it returns a function
#    rather than doing the work immediately.
##########################################################################
global def pythonInstaller pipdir Unit =
  installPipenvEnv pipdir | verifyFiles



#######################################################################################
# Commands for running Python. We suppress byte code since it causes concurrency problems with Wake.
######################################################################################
global def pythonCommand script args = ("python3.7", "-B", script, args)
global def pythonModule module args = ("python3.7", "-B", "-m", module, args)



##############################################################################
# Install the python environment associated with Pipfile.lock.
#  Note we install pipenv first, then use pipenv to install the remaining modules.
#     For the workaround, the first step returns one file, and the second step
#     returns all the files except that one.
#     Appended together, we have a complete set of files.
################################################################################
target installPipenvEnv pipDir =

  # Where we will install the virtual environment.
  def venv = "build/{pipDir}/.venv"
  def pipFile = installIn "build" (source "{pipDir}/Pipfile")
  def lock = installIn "build" (source "{pipDir}/Pipfile.lock")
  def installPipenvScript = source "{here}/install-python-pipenv"

  def args =
    installPipenvScript.getPathName,
    pipFile.getPathName,
    lock.getPathName,
    venv,
    Nil

  def visible = installPipenvScript, pipFile, lock, Nil

  makePlan args visible
  | setPlanResources ("python/python/3.7.1", Nil)
  | runJob
  | getJobOutputs



# Remove a string from a list of strings.
def remove string strings =
  filter (_ !=~ string) strings
