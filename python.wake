############################################################################
#
# Python-Wake
#
# Supports multiple Python virtual environments through the "pipenv" mechanism.
#   Each environment corresponds to a directory containing a Pipfile.
#
# To use the python virtual environment, add it to the command's plan.
# For example,
#     makePlan  (pythonCommand  "myscript.py" (arg1, ar2, Nil))  visibles
#     | addPythonEnv  "directory containing Pipfile"
#     | runJob
#
# If you have a specific python module to run, you can
#     makePlan (python my.module (arglist))  sources
#
# If you wish to have the python modules downloaded by tne "preinstall" function.
#      publish preinstall = (pythonInstaller "directory-containing-Pipfile"), Nil
#
#############################################################################
global tuple PythonEnv =
  global Pipfile: String
  global Venv: String



######################################################
# Add a Python virtual environment to a plan.
#   Currently only supports pipenv, but this is where would examine the directory
#   to see if other env managers are being used.
##########################################################
global def addPythonEnv env plan =

    # Install the virtual environment if not already done
    def installed = installPythonEnv env

    # Add the binary directory to PATH.
    plan
    | editPlanVisible (installed ++ _)
    | addPlanEnvironmentPath  "PYTHONPATH"  env.getPythonEnvVenv
    | addPlanEnvironmentPath  "PATH" "{env.getPythonEnvVenv}/bin"


###########################################################################
# Create an installer function which, when invoked, installs python virtual env.
#    Note the extra "_" in the argument list
##########################################################################
global def pythonInstaller env Unit =
  installPythonEnv env | verifyFiles



#######################################################################################
# Commands for running Python.  We suppress byte code since it causes concurrency problems with Wake.
######################################################################################
global def pythonCommand script args = ("python3", "-B", script, args)
global def pythonModule module args = ("python3", "-B", "-m", module, args)



##############################################################################
# Install the python environment associated with Pipfile.lock.
#  Note we install pipenv first, then use pipenv to install the remaining modules.
#     For the workaround, the first step returns one file, and the second step
#     returns all the files except that one.
#     Appended together, we have a complete set of files.
################################################################################
target installPythonEnv env =

   # Where we will install the virtual environment.
   def vdir = simplify "{env.getPythonEnvVenv}/.."  # The directory containing the viritual environment
   def venv = relative vdir env.getPythonEnvVenv # The name of the virtual environment within the directory. ".venv"
   def lock = "{env.getPythonEnvPipfile}.lock" | debug "pipfile.lock"

   # Copy Pipfile.lock to the installation directory.
   def newDir = mkdir vdir | debug "newDir"
   def newLock = source lock | installAs "{vdir}/Pipfile.lock" | debug "newLock"

   # Running from the build directory, use pip to install pipenv in .venv/bin
   def pipOutputs =
      makePlan ("pip3", "install", "--target={venv}", "pipenv", Nil) (newLock, newDir, Nil)  # May be creating destination directory.
      | setPlanLocalOnly True   # TODO: Remove when wake problem is fixed. (see node.wake)
      | setPlanFnOutputs (env.getPythonEnvVenv, _)  # TODO: Remove when wake problem is fixed.
      | setPlanResources ("python/python/3.7.1", Nil)
      | setPlanDirectory vdir
      | runJob
      | getJobOutputs | debug "pipOutputs"

   # Use pipenv and Pipfile.lock to install everything else in .venv.
   def pipenvOutputs =
     makePlan ("{venv}/bin/pipenv",  "sync", "--dev", Nil) (newLock, pipOutputs)
     | setPlanLocalOnly True       # TODO: remove when wake problem is resolved
     | setPlanFnOutputs ( \_ remove venv (files venv `.*`) ) # TODO: remove when wake problem is resolved
     | setPlanResources ("python/python/3.7.1", Nil)
     | setPlanEnvironmentVar "PIPENV_VENV_IN_PROJECT" "YES"
     | setPlanDirectory vdir
     | runJob
     | getJobOutputs

   # Our return value is all of the files installed.
   pipenvOutputs ++ pipOutputs



# Remove a string from a list of strings.
def remove string strings =
    filter (_ !=~ string) strings
